; Copyright (c) uib gmbh (www.uib.de)
; This sourcecode is owned by uib gmbh
; and published under the Terms of the General Public License.
; credits: http://www.opsi.org/en/credits/

;setloglevel=7

;==========================================================================================================
; Variables
DefVar $UninstMsiId$
DefVar $UninstallString$
DefVar $UninstallProgram$
DefStringList $MsiIds$

Set $MsiIds$ = addtolist($MsiIds$, "{5676F50B-9B69-415A-ACB5-E591BF48D282}")
Set $MsiIds$ = addtolist($MsiIds$, "{E310D575-C678-46BB-9024-B68E618A398B}")

;==========================================================================================================
; Program logic
Message "Uninstalling " + $ProductId$ + " ..."
Killtask "PoP.exe"
for %actmsi% in $MsiIds$ do sub_remove_by_msiid
comment "Delete program shortcuts"
LinkFolder_uninstall

;==========================================================================================================
; Functions
[sub_remove_by_msiid]
Set $UninstMsiId$ = "%actmsi%"
Set $UninstallString$ = GetRegistryStringValue64("[HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\%actmsi%] QuietUninstallString")
Set $UninstallProgram$ = takeString(0, splitString ($UninstallString$," "))
if FileExists($UninstallProgram$)
  comment $ProductId$ + " installation found, starting uninstall"
  Winbatch_uninstall
  sub_check_exitcode
endif

[Winbatch_uninstall]
$UninstallString$

[LinkFolder_uninstall]
set_basefolder common_desktopdirectory
set_subfolder ""
delete_element $DesktopLinkName$

[Sub_check_exitcode]
comment "Test for installation success via exit code"
set $ExitCode$ = getLastExitCode
; informations to exit codes see
; http://msdn.microsoft.com/en-us/library/aa372835(VS.85).aspx
; http://msdn.microsoft.com/en-us/library/aa368542.aspx
if ($ExitCode$ = "0")
	comment "Looks good: setup program gives exitcode zero"
else
	comment "Setup program gives a exitcode unequal zero: " + $ExitCode$
	if ($ExitCode$ = "1605")
		comment "ERROR_UNKNOWN_PRODUCT	1605	This action is only valid for products that are currently installed."
		comment "Uninstall of a not installed product failed - no problem"
	else
		if ($ExitCode$ = "1641")
			comment "looks good: setup program gives exitcode 1641"
			comment "ERROR_SUCCESS_REBOOT_INITIATED	1641	The installer has initiated a restart. This message is indicative of a success."
		else
			if ($ExitCode$ = "3010")
				comment "looks good: setup program gives exitcode 3010"
				comment "ERROR_SUCCESS_REBOOT_REQUIRED	3010	A restart is required to complete the install. This message is indicative of a success."
			else
				logError "Fatal: Setup program gives an unknown exitcode unequal zero: " + $ExitCode$
				isFatalError
			endif
		endif
	endif
endif